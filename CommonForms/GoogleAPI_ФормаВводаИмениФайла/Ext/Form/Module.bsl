
&НаСервере
Функция ОшибкиЗаполнения()
	
	МассивОшибок = Новый Массив;
	МассивОшибок.Добавить("Сохранение невозможно по причине:");
	
	Если ПустаяСтрока(ИмяФайла) Тогда
		МассивОшибок.Добавить("Не заполнено имя файла");
	КонецЕсли;
	Если ПустаяСтрока(ТипФайла) Тогда 
		МассивОшибок.Добавить("Не заполнен тип файла");
	КонецЕсли;
	
	Если МассивОшибок.Количество() > 1 Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрСоединить(МассивОшибок, Символы.ПС));
		Возврат Истина;
	Иначе 
		Возврат Ложь;
	КонецЕсли;	
	
КонецФункции	

&НаКлиенте
Процедура ОК(Команда)
	Если ОшибкиЗаполнения() Тогда 
		Возврат;
	КонецЕсли;	
	ТекСтр = Элементы.ДеревоПапок.ТекущиеДанные;
	ЭтаФорма.Закрыть(Новый Структура("ИмяФайла, ТипФайла, id_папки, TeamDrives", ИмяФайла, ТипФайла, ТекСтр.id, ?(ВидДиска = 1, "true", "false")));
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	ЭтаФорма.Закрыть(Ложь);
КонецПроцедуры

&НаСервере
Функция ДеревоДляПередачи(ДеревоНаСервере, СтруктураТекСтроки)
	
	Если ВидДиска = 0 И (СтруктураТекСтроки = Неопределено 
			ИЛИ Не СтруктураТекСтроки = Неопределено И СтруктураТекСтроки.Нажатие = 0 И СтруктураТекСтроки.id_Родителя = "") Тогда 
		
		НоваяСтрока = ДеревоНаСервере.Строки.Добавить();
		НоваяСтрока.id = "";
		НоваяСтрока.Наименование = "Мой диск";
		НоваяСтрока.id_Родителя = "";
		НоваяСтрока.Нажатие = 1;
		
		Если Не СтруктураТекСтроки = Неопределено Тогда 
			СтруктураТекСтроки.Вставить("id_Папки", "");
		КонецЕсли;	
	
		Возврат НоваяСтрока;
		
	ИначеЕсли Не СтруктураТекСтроки = Неопределено И СтруктураТекСтроки.Нажатие = 1 Тогда 
		
		НоваяСтрока = ДеревоНаСервере.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураТекСтроки);
		СтруктураТекСтроки.Вставить("id_Папки", СтруктураТекСтроки.id);
		Возврат НоваяСтрока;
		
	ИначеЕсли Не СтруктураТекСтроки = Неопределено И СтруктураТекСтроки.Нажатие = 0 Тогда 
		
		Фабрика = GoogleAPI_ПроцедурыРаботыСGDrive.ПолучитьФайлПоID(Параметры.ПараметрыПользователя, СтруктураТекСтроки.id_Родителя, ?(ВидДиска = 1, "true", "false"));		
		
		НоваяСтрока = ДеревоНаСервере.Строки.Добавить();
		НоваяСтрока.id = Фабрика.id;
		НоваяСтрока.Наименование = Фабрика.name;
		Если Фабрика.Свойство("parents") Тогда 
			НоваяСтрока.id_Родителя = Фабрика.parents.Получить(0);
		Иначе 	
			НоваяСтрока.id_Родителя = "";
		КонецЕсли;	
		НоваяСтрока.Нажатие = 1;
		
		СтруктураТекСтроки.Вставить("id_Папки", СтруктураТекСтроки.id_Родителя);
		
		Возврат НоваяСтрока;
	
	КонецЕсли;
	
	Возврат ДеревоНаСервере;
	
КонецФункции	

&НаСервере
Процедура ВидДискаПриИзмененииНаСервере(СтруктураТекСтроки = Неопределено)
	
	//заполнить дерево
	// ВидДиска = 0 - личные папки
	// ВидДиска = 1 - общий диск
	
	ДеревоНаСервере = РеквизитФормыВЗначение("ДеревоПапок");
	ДеревоНаСервере.Строки.Очистить();
	
	GoogleAPI_ПроцедурыРаботыСGDrive.ПолучитьРекурсивноСписокПапок(Параметры.ПараметрыПользователя, 
							ДеревоДляПередачи(ДеревоНаСервере, СтруктураТекСтроки), 
							ВидДиска = 1, 
							?(СтруктураТекСтроки = Неопределено, "", СтруктураТекСтроки.id_Папки));
	
	ЗначениеВРеквизитФормы(ДеревоНаСервере, "ДеревоПапок");
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДискаПриИзменении(Элемент)
	ВидДискаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ВидДискаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПапокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекСтр = Элементы.ДеревоПапок.ТекущиеДанные;

	Если Не Элемент.ТекущийЭлемент = Неопределено И Не ТекСтр = Неопределено Тогда 
		
		ТекСтр.Нажатие = ?(ТекСтр.Нажатие = 0, 1, 0);
		СтруктураТекСтроки = Новый Структура("ID, Наименование, id_Родителя, Нажатие", ТекСтр.ID, ТекСтр.Наименование, ТекСтр.id_Родителя, ТекСтр.Нажатие);
		ВидДискаПриИзмененииНаСервере(СтруктураТекСтроки);
		
	КонецЕсли;	
	
КонецПроцедуры
