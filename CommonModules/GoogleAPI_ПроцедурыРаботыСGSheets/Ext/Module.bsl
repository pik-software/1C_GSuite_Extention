
Функция СоздатьGoogleSpreadsheet(ПараметрыПользователя, ИмяТаблицы, КоличествоЛистов = 1) Экспорт
	
	ТаблицаGSs = Неопределено;
	
	ЗафиксированыОшибки = Ложь;
	
    СтруктураПараметров = GoogleAPI_ОбщегоНазначения.ПодготовкаСоединенияПоAPI(ПараметрыПользователя, "СерверGSheets");
	Если СтруктураПараметров = Неопределено Тогда 
    	Возврат СтруктураПараметров;
    КонецЕсли;	
	
	Попытка
		
		//создание новой таблицы
		Соединение = Новый HTTPСоединение(СтруктураПараметров.СерверGSheets,443,,,,,Новый ЗащищенноеСоединениеOpenSSL);
		
		СтруктураПараметров.Заголовки.Вставить("Content-Type", "application/json");	
		СтрокаЗапроса = СтруктураПараметров.РесурсGSheets + "/?&access_token=" + ПараметрыПользователя.ТокенДоступа;
		
		ЗапросHTTP = Новый HTTPЗапрос(СтрокаЗапроса, СтруктураПараметров.Заголовки);
		ЗапросHTTP.УстановитьТелоИзСтроки(СформироватьТелоЗапросаСозданиеТаблицыGSs(ИмяТаблицы, КоличествоЛистов));
		ОтветHTTP = Соединение.ВызватьHTTPМетод("POST", ЗапросHTTP);
		
		Если НЕ ОтветHTTP.КодСостояния = 200 Тогда
			Сообщить("Ошибка, код HTTP: " + ОтветHTTP.КодСостояния);
			
			ЗафиксированыОшибки = Истина;
		 
			Если НЕ ЗапросHTTP = Неопределено И НЕ ОтветHTTP = Неопределено Тогда
				ТекстОшибок = "Ошибка HTTP запроса " + ОтветHTTP.ПолучитьТелоКакСтроку() + ", код ошибки " + ОтветHTTP.КодСостояния;
				GoogleAPI_ОбщегоНазначения.ЗаписьОшибкиПриРаботеСGS(ТекстОшибок, ".Создание таблицы");
			КонецЕсли; 
			
			Возврат ТаблицаGSs;
			
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = ОписаниеОшибки();
		GoogleAPI_ОбщегоНазначения.ЗаписьОшибкиПриРаботеСGS(ТекстОшибки, ".Создание таблицы");
				
		Возврат ТаблицаGSs;		
				
	КонецПопытки; 
	
	ОтветHTTPТело = ОтветHTTP.ПолучитьТелоКакСтроку();
	
	Чтение = Новый ЧтениеJSON();
	Чтение.УстановитьСтроку(ОтветHTTPТело);

	ТаблицаGSsXDTO = ФабрикаXDTO.ПрочитатьJSON(Чтение);
	
	Чтение.Закрыть();
	
	ТаблицаGSs = Новый Структура;
	ТаблицаGSs.Вставить("title",			 ТаблицаGSsXDTO.properties.title);
	ТаблицаGSs.Вставить("spreadsheetId",	 ТаблицаGSsXDTO.spreadsheetId);
	ТаблицаGSs.Вставить("spreadsheetUrl",	 ТаблицаGSsXDTO.spreadsheetUrl);
	
	Возврат ТаблицаGSs;
	
КонецФункции

Функция ЗаполнитьGoogleSpreadSheet(ТаблицаGSs, GSsОбласть, ТелоЗапросаJSON, ПараметрыПользователя) Экспорт
	
	ЗафиксированыОшибки = Ложь;
	
    СтруктураПараметров = GoogleAPI_ОбщегоНазначения.ПодготовкаСоединенияПоAPI(ПараметрыПользователя, "СерверGSheets");
	Если СтруктураПараметров = Неопределено Тогда 
    	Возврат СтруктураПараметров;
    КонецЕсли;	
	
	Попытка
		
		Соединение = Новый HTTPСоединение(СтруктураПараметров.СерверGSheets,443,,,,,Новый ЗащищенноеСоединениеOpenSSL);
		
		СтрокаЗапроса = СтруктураПараметров.РесурсGSheets + ТаблицаGSs.spreadsheetId + "/values/" + GSsОбласть + "?"
				+ "&valueInputOption=USER_ENTERED"
				+ "&access_token=" + ПараметрыПользователя.ТокенДоступа;
				
		СтруктураПараметров.Заголовки.Вставить("Content-Type", "application/json");	
			
		ЗапросHTTP = Новый HTTPЗапрос(СтрокаЗапроса, СтруктураПараметров.Заголовки);
		
		ЗапросHTTP.УстановитьТелоИзСтроки(ТелоЗапросаJSON);
		
		ОтветHTTP = Соединение.ВызватьHTTPМетод("PUT", ЗапросHTTP);
		
		Если НЕ ОтветHTTP.КодСостояния = 200 Тогда
			
			Сообщить("Ошибка, код HTTP: " + ОтветHTTP.КодСостояния);
			
			ЗафиксированыОшибки = Истина;
			
			Если НЕ ЗапросHTTP = Неопределено И НЕ ОтветHTTP = Неопределено Тогда
				ТекстОшибок = "Ошибка HTTP запроса " + ОтветHTTP.ПолучитьТелоКакСтроку() + ", код ошибки " + ОтветHTTP.КодСостояния;
		    	GoogleAPI_ОбщегоНазначения.ЗаписьОшибкиПриРаботеСGS(ТекстОшибок, ".Заполнение таблицы");
			КонецЕсли; 
			
			Возврат ЗафиксированыОшибки;
			
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации("GoogleSpreadsheet.Заполнение таблицы данными", УровеньЖурналаРегистрации.Ошибка, 
				, , ТекстОшибки); 
				
		Возврат ЗафиксированыОшибки;
				
	КонецПопытки; 
	
	Возврат ЗафиксированыОшибки;
	
КонецФункции

Функция ОтформатироватьТаблицуGSsОСВ(ТаблицаGSs, ЗапросJSON, ПараметрыПользователя) Экспорт
	
	ЗафиксированыОшибки = Ложь;
	
    СтруктураПараметров = GoogleAPI_ОбщегоНазначения.ПодготовкаСоединенияПоAPI(ПараметрыПользователя, "СерверGSheets");
	Если СтруктураПараметров = Неопределено Тогда 
    	Возврат СтруктураПараметров;
    КонецЕсли;	
	
	Попытка
		
		Соединение = Новый HTTPСоединение(СтруктураПараметров.СерверGSheets, 443,,,,,Новый ЗащищенноеСоединениеOpenSSL);
		
		СтрокаЗапроса = СтруктураПараметров.РесурсGSheets + ТаблицаGSs.spreadsheetId + ":batchUpdate?"
				+ "&access_token=" + ПараметрыПользователя.ТокенДоступа;
				
		СтруктураПараметров.Заголовки.Вставить("Content-Type", "application/json");	
			
		ЗапросHTTP = Новый HTTPЗапрос(СтрокаЗапроса, СтруктураПараметров.Заголовки);
		
		ЗапросHTTP.УстановитьТелоИзСтроки(ЗапросJSON);
		
		ОтветHTTP = Соединение.ВызватьHTTPМетод("POST", ЗапросHTTP);
		
		Если НЕ ОтветHTTP.КодСостояния = 200 Тогда
			
			Сообщить("Ошибка, код HTTP: " + ОтветHTTP.КодСостояния);
			
			ЗафиксированыОшибки = Истина; 
			
			Если НЕ ЗапросHTTP = Неопределено И НЕ ОтветHTTP = Неопределено Тогда
				
				ТекстОшибок = "Ошибка HTTP запроса " + ОтветHTTP.ПолучитьТелоКакСтроку() + ", код ошибки " + ОтветHTTP.КодСостояния;
		    	GoogleAPI_ОбщегоНазначения.ЗаписьОшибкиПриРаботеСGS(ТекстОшибок, ".Заполнение таблицы данными");
				
			КонецЕсли; 
			
			Возврат ЗафиксированыОшибки;
			
		КонецЕсли;
				
	Исключение
		
		ТекстОшибки = ОписаниеОшибки();
		GoogleAPI_ОбщегоНазначения.ЗаписьОшибкиПриРаботеСGS(ТекстОшибки, ".Заполнение таблицы данными");
				
		Возврат ЗафиксированыОшибки;		
								
	КонецПопытки; 
	
	Возврат ЗафиксированыОшибки;
	
КонецФункции 




#Область ВспомогательныеФункции

Функция ПолучитьОбластьДляТаблицы(ТаблицаДляТелаЗапроса, ЛистGS, НомерНачальногоСимволаСтолбца = 65, НомерНачальнойСтроки = 1) Экспорт
	
	СтруктураОбластей = Новый Структура;
	
	НомерСимвола = НомерНачальногоСимволаСтолбца - 1 + ТаблицаДляТелаЗапроса.Колонки.Количество();
	
	СтруктураОбластей.Вставить("ОбластьШапки", ЛистGS + "!" + Символ(НомерНачальногоСимволаСтолбца) + Строка(НомерНачальнойСтроки) + ":" + Символ(НомерСимвола) + "1");
	СтруктураОбластей.Вставить("ОбластьТела", ЛистGS + "!" + Символ(НомерНачальногоСимволаСтолбца) + Строка(НомерНачальнойСтроки + 1) + ":" + Символ(НомерСимвола) + "" + (ТаблицаДляТелаЗапроса.Количество() + 1));
	
	Возврат СтруктураОбластей;
	
КонецФункции	

Функция СформироватьТелоЗапросаФорматированиеТаблицыGSsВыравниваниеКолонок(ЛистИД, ПерваяКолонка = Неопределено, ПоследняяКолонка = Неопределено, 
		ПерваяСтрока = Неопределено, ПоследняяСтрока = Неопределено, ГоризонтальноеПоложение = "LEFT", ВертикальноеПоложение = "TOP") Экспорт 
	
	ФабрикаGSs = ФабрикаXDTO;
	
	ТипBatchUpdateRepeatCell	 = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "BatchUpdateRepeatCell");
	
	ОбъектBatchUpdateRepeatCell = ФабрикаGSs.Создать(ТипBatchUpdateRepeatCell);
	
	//..форматирование переноса слов в колоноках
	
	ОбъектBatchRequestRepeatCell = GoogleAPI_СериализацияОбъектовGoogleJSON.ПолучитьОбъектBatchRequestRepeatCellВыравнивание(ФабрикаGSs, ЛистИД, ПерваяКолонка, ПоследняяКолонка, ПерваяСтрока, ПоследняяСтрока, ГоризонтальноеПоложение, ВертикальноеПоложение);
	ОбъектBatchUpdateRepeatCell.requests.Добавить(ОбъектBatchRequestRepeatCell);
	
	ЗаписьJSON	= Новый ЗаписьJSON;
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);
	
	ФабрикаGSs.ЗаписатьJSON(ЗаписьJSON, ОбъектBatchUpdateRepeatCell, НазначениеТипаXML.Неявное);
	ТелоЗапросаJSON = ЗаписьJSON.Закрыть();
	
	ТелоЗапросаJSON = GoogleAPI_СериализацияОбъектовGoogleJSON.ПолучитьЗначениеОбъектаJSON(ТелоЗапросаJSON);
	
	Возврат ТелоЗапросаJSON;
	
КонецФункции

#КонецОбласти 

#Область ФормированиеТелоЗапросаGoogle

Функция ПолучитьТелоЗапросаJSON_ДляШапкиТаблицы(ТаблицаДляТелаЗапроса, GSsОбласть) Экспорт
	
	ФабрикаGSs = ФабрикаXDTO;
	
	//..формирование тела запроса
	ДопПараметры = Новый Структура;
	
	ТипValueRange = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "ValueRange");
	ТипValueRangeValues = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "ValueRangeValues");
	
	ОбъектValueRange = ФабрикаGSs.Создать(ТипValueRange);
	
	ОбъектValueRange.range			 = GSsОбласть;
	ОбъектValueRange.majorDimension	 = "ROWS";
	
	//..заполнение строк шапки
	ОбъектValueRangeValues = ФабрикаGSs.Создать(ТипValueRangeValues);
	Для Каждого Колонка Из ТаблицаДляТелаЗапроса.Колонки Цикл 
		ОбъектValueRangeValues.values.Добавить(?(ЗначениеЗаполнено(Колонка.Заголовок), Колонка.Заголовок, Колонка.Имя));
	КонецЦикла;	
	ОбъектValueRange.values.Добавить(ОбъектValueRangeValues);
		
	ЗаписьJSON	= Новый ЗаписьJSON;
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);
	
	ФабрикаGSs.ЗаписатьJSON(ЗаписьJSON, ОбъектValueRange, НазначениеТипаXML.Неявное);
	ТелоЗапросаJSON = ЗаписьJSON.Закрыть();
	
	ТелоЗапросаJSON = GoogleAPI_СериализацияОбъектовGoogleJSON.ПолучитьЗначениеОбъектаJSON(ТелоЗапросаJSON);
	
	Возврат ТелоЗапросаJSON;	

КонецФункции

Функция ПолучитьТелоЗапросаJSON_ДляТаблицы(ТаблицаДляТелаЗапроса, GSsОбласть) Экспорт
	
	ФабрикаGSs = ФабрикаXDTO;
	
	//..формирование тела запроса
	ДопПараметры = Новый Структура;
	
	ТипValueRange = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "ValueRange");
	ТипValueRangeValues = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "ValueRangeValues");
	
	ОбъектValueRange = ФабрикаGSs.Создать(ТипValueRange);
	
	ОбъектValueRange.range			 = GSsОбласть;
	ОбъектValueRange.majorDimension	 = "ROWS";
	
	//..заполнение строк шапки
	Для Каждого Стр Из ТаблицаДляТелаЗапроса Цикл 
		
		ОбъектValueRangeValues = ФабрикаGSs.Создать(ТипValueRangeValues);
		Для Каждого Колонка Из ТаблицаДляТелаЗапроса.Колонки Цикл 
			ОбъектValueRangeValues.values.Добавить(Стр[Колонка.Имя]);
		КонецЦикла;	
		ОбъектValueRange.values.Добавить(ОбъектValueRangeValues);
		
	КонецЦикла;	
		
	ЗаписьJSON	= Новый ЗаписьJSON;
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);
	
	ФабрикаGSs.ЗаписатьJSON(ЗаписьJSON, ОбъектValueRange, НазначениеТипаXML.Неявное);
	ТелоЗапросаJSON = ЗаписьJSON.Закрыть();
	
	ТелоЗапросаJSON = GoogleAPI_СериализацияОбъектовGoogleJSON.ПолучитьЗначениеОбъектаJSON(ТелоЗапросаJSON);
	
	Возврат ТелоЗапросаJSON;	

КонецФункции

//Параметры:
//	- цвет - структура
//Пример:
//Цвет = Новый Структура;
//Цвет.Вставить("Красный", Формат(244/255, "ЧДЦ=15; ЧРД=."));
//Цвет.Вставить("Зеленый", Формат(236/255, "ЧДЦ=15; ЧРД=."));
//Цвет.Вставить("Синий", Формат(197/255, "ЧДЦ=15; ЧРД=."));
//
//
Функция СформироватьТелоЗапросаФорматированиеТаблицыGSsИзменениеЦвета(ЛистИД, ПерваяКолонка, ПоследняяКолонка, ПерваяСтрока, ПоследняяСтрока, Цвет) Экспорт 
	
	ФабрикаGSs = ФабрикаXDTO;
	
	ТипBatchUpdateRepeatCell	 = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "BatchUpdateRepeatCell");
	
	ОбъектBatchUpdateRepeatCell = ФабрикаGSs.Создать(ТипBatchUpdateRepeatCell);
	
	//..форматирование фона ячеек
		
	ОбъектBatchRequestRepeatCell = GoogleAPI_СериализацияОбъектовGoogleJSON.ПолучитьОбъектBatchRequestRepeatCellФонЯчейки(ФабрикаGSs, ЛистИД, ПерваяКолонка, ПоследняяКолонка, ПерваяСтрока, ПоследняяСтрока, Цвет);
	ОбъектBatchUpdateRepeatCell.requests.Добавить(ОбъектBatchRequestRepeatCell);
	
	ЗаписьJSON	= Новый ЗаписьJSON;
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);
	
	ФабрикаGSs.ЗаписатьJSON(ЗаписьJSON, ОбъектBatchUpdateRepeatCell, НазначениеТипаXML.Неявное);
	ТелоЗапросаJSON = ЗаписьJSON.Закрыть();
	
	ТелоЗапросаJSON = GoogleAPI_СериализацияОбъектовGoogleJSON.ПолучитьЗначениеОбъектаJSON(ТелоЗапросаJSON);
	
	Возврат ТелоЗапросаJSON;
	
КонецФункции

Функция СформироватьТелоЗапросаФорматированиеТаблицыGSsШиринаКолонок(ЛистИД, ПерваяКолонка, ПоследняяКолонка, ШиринаКолонки)
	
	ФабрикаGSs = ФабрикаXDTO;
	
	//..форматирование ширины колонок
	ТипBatchUpdateDimensionProperties	 = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "BatchUpdateDimensionProperties");
	
	ОбъектBatchUpdateDimensionProperties = ФабрикаGSs.Создать(ТипBatchUpdateDimensionProperties);
	
	ОбъектBatchRequestUpdateDimensionProperties = GoogleAPI_СериализацияОбъектовGoogleJSON.ПолучитьОбъектЗапросаУстановкаШириныКолонки(ФабрикаGSs, ЛистИД, ПерваяКолонка, ПоследняяКолонка, ШиринаКолонки);
	ОбъектBatchUpdateDimensionProperties.requests.Добавить(ОбъектBatchRequestUpdateDimensionProperties);
		
	ЗаписьJSON	= Новый ЗаписьJSON;
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);
	
	ФабрикаGSs.ЗаписатьJSON(ЗаписьJSON, ОбъектBatchUpdateDimensionProperties, НазначениеТипаXML.Неявное);
	ТелоЗапросаJSON = ЗаписьJSON.Закрыть();
	
	ТелоЗапросаJSON = GoogleAPI_СериализацияОбъектовGoogleJSON.ПолучитьЗначениеОбъектаJSON(ТелоЗапросаJSON);
	
	Возврат ТелоЗапросаJSON;
	
КонецФункции

Функция СформироватьТелоЗапросаФорматированиеТаблицыGSsФиксацияСтрокИКолонок(ЛистИД, КоличествоЗафиксСтрок, КоличествоЗафиксКолонок) Экспорт 
	
	ФабрикаGSs = ФабрикаXDTO;
	
	ТипBatchUpdateSheetProperties		 = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "BatchUpdateSheetProperties");
	ТипBatchUpdateSheetPropertiesRequest = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "BatchUpdateSheetPropertiesRequest");
	ТипUpdateSheetPropertiesRequest		 = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "UpdateSheetPropertiesRequest");
	ТипSheetProperties					 = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "SheetProperties");
	ТипGridProperties					 = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "GridProperties");
	
	ОбъектGridProperties = ФабрикаGSs.Создать(ТипGridProperties);
	ОбъектGridProperties.frozenRowCount		 = КоличествоЗафиксСтрок;
	ОбъектGridProperties.frozenColumnCount	 = КоличествоЗафиксКолонок;
	
	ОбъектSheetProperties = ФабрикаGSs.Создать(ТипSheetProperties);
	ОбъектSheetProperties.sheetId		 = ЛистИД;
	ОбъектSheetProperties.gridProperties = ОбъектGridProperties;
	
	ОбъектUpdateSheetPropertiesRequest = ФабрикаGSs.Создать(ТипUpdateSheetPropertiesRequest);
	ОбъектUpdateSheetPropertiesRequest.properties	 = ОбъектSheetProperties;
	ОбъектUpdateSheetPropertiesRequest.fields		 = "gridProperties(frozenRowCount,frozenColumnCount)";
	
	ОбъектBatchUpdateSheetPropertiesRequest = ФабрикаGSs.Создать(ТипBatchUpdateSheetPropertiesRequest);
	ОбъектBatchUpdateSheetPropertiesRequest.updateSheetProperties = ОбъектUpdateSheetPropertiesRequest;
	
	ОбъектBatchUpdateSheetProperties = ФабрикаGSs.Создать(ТипBatchUpdateSheetProperties);
	ОбъектBatchUpdateSheetProperties.requests.Добавить(ОбъектBatchUpdateSheetPropertiesRequest);
	
	ЗаписьJSON	= Новый ЗаписьJSON;
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);
	
	ФабрикаGSs.ЗаписатьJSON(ЗаписьJSON, ОбъектBatchUpdateSheetProperties, НазначениеТипаXML.Неявное);
	ТелоЗапросаJSON = ЗаписьJSON.Закрыть();
	
	ТелоЗапросаJSON = GoogleAPI_СериализацияОбъектовGoogleJSON.ПолучитьЗначениеОбъектаJSON(ТелоЗапросаJSON);
	
	Возврат ТелоЗапросаJSON;

КонецФункции

Функция СформироватьТелоЗапросаОбъединенияЯчеек(ЛистИД, ПерваяКолонка, ПоследняяКолонка, ПерваяСтрока, ПоследняяСтрока, ТипОбъединения = "MERGE_ALL")
	
	ФабрикаGSs = ФабрикаXDTO;
	
	ТипBatchUpdateMergeCell	 = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "BatchUpdateMergeCell");
	
	ОбъектBatchUpdateMergeCell = ФабрикаGSs.Создать(ТипBatchUpdateMergeCell);
	
	ОбъектBatchRequestMergeCell = GoogleAPI_СериализацияОбъектовGoogleJSON.ПолучитьОбъектОбъединениеЯчеек(ФабрикаGSs, ЛистИД, ПерваяКолонка, ПоследняяКолонка, ПерваяСтрока, ПоследняяСтрока, ТипОбъединения);
	ОбъектBatchUpdateMergeCell.requests.Добавить(ОбъектBatchRequestMergeCell);
	
	ЗаписьJSON	= Новый ЗаписьJSON;
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);
	
	ФабрикаGSs.ЗаписатьJSON(ЗаписьJSON, ОбъектBatchUpdateMergeCell, НазначениеТипаXML.Неявное);
	ТелоЗапросаJSON = ЗаписьJSON.Закрыть();
	
	ТелоЗапросаJSON = GoogleAPI_СериализацияОбъектовGoogleJSON.ПолучитьЗначениеОбъектаJSON(ТелоЗапросаJSON);
	
	Возврат ТелоЗапросаJSON;	
	
КонецФункции

Функция ПолучитьТелоЗапросаJSON_ДляТабличногоДокумента(ТабДок, GSsОбласть, ПерваяСтрока = 1, ПерваяКолонка = 1, ПоследняяСтрока = Неопределено, ПоследняяКолонка = Неопределено) Экспорт
	
	ФабрикаGSs = ФабрикаXDTO;
	
	//..формирование тела запроса
	ДопПараметры = Новый Структура;
	
	ТипValueRange = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "ValueRange");
	ТипValueRangeValues = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "ValueRangeValues");
	
	ОбъектValueRange = ФабрикаGSs.Создать(ТипValueRange);
	
	ОбъектValueRange.range			 = GSsОбласть;
	ОбъектValueRange.majorDimension	 = "ROWS";
	
	Если ПоследняяСтрока = Неопределено Тогда 
		ПоследняяСтрока = ТабДок.ВысотаТаблицы;
	КонецЕсли;
	Если ПоследняяКолонка = Неопределено Тогда 
		ПоследняяКолонка = ТабДок.ШиринаТаблицы;
	КонецЕсли;	
	
	//..заполнение строк шапки
	Для Строка = ПерваяСтрока По ПоследняяСтрока Цикл 
		
		ОбъектValueRangeValues = ФабрикаGSs.Создать(ТипValueRangeValues);
		Для Колонка = ПерваяКолонка По ПоследняяКолонка Цикл 
			ОбъектValueRangeValues.values.Добавить(СокрЛП(ТабДок.Область("R" + Формат(Строка, "ЧГ=0") + "C" + Формат(Колонка, "ЧГ=0")).Значение));
		КонецЦикла;	
		ОбъектValueRange.values.Добавить(ОбъектValueRangeValues);
		
	КонецЦикла;	
		
	ЗаписьJSON	= Новый ЗаписьJSON;
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);
	
	ФабрикаGSs.ЗаписатьJSON(ЗаписьJSON, ОбъектValueRange, НазначениеТипаXML.Неявное);
	ТелоЗапросаJSON = ЗаписьJSON.Закрыть();
	
	ТелоЗапросаJSON = GoogleAPI_СериализацияОбъектовGoogleJSON.ПолучитьЗначениеОбъектаJSON(ТелоЗапросаJSON);
	
	Возврат ТелоЗапросаJSON;	

КонецФункции

Функция СформироватьТелоЗапросаСозданиеТаблицыGSs(ИмяТаблицы, КоличествоЛистов)
	
	ФабрикаGSs = ФабрикаXDTO;
	
	//..формирование тела запроса
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("КоличествоЛистов", КоличествоЛистов);
	
	ТипSpreadsheet = ФабрикаGSs.Тип("http://www.googlespreadsheet.org", "Spreadsheet");
	ОбъектSpreadsheet = ФабрикаGSs.Создать(ТипSpreadsheet);
	GoogleAPI_СериализацияОбъектовGoogleJSON.ЗаполнитьОбъектПоУмолчаниюGSs(ОбъектSpreadsheet, ФабрикаGSs, ДопПараметры);
	
	ОбъектSpreadsheet.properties.title = ИмяТаблицы;
	
	ЗаписьJSON	= Новый ЗаписьJSON;
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);
	
	ФабрикаGSs.ЗаписатьJSON(ЗаписьJSON, ОбъектSpreadsheet);
	ТелоЗапросаJSON = ЗаписьJSON.Закрыть();
	
	ТелоЗапросаJSON = GoogleAPI_СериализацияОбъектовGoogleJSON.ПолучитьЗначениеОбъектаJSON(ТелоЗапросаJSON);
	
	Возврат ТелоЗапросаJSON;
	
КонецФункции

#КонецОбласти



